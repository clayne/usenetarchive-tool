# Maintainer: Bartosz Taudul <wolf@nereid.pl>
pkgname=uat-git
pkgver=1
pkgrel=1
pkgdesc="Usenet Archive Toolkit"
arch=('x86_64' 'aarch64')
url="https://github.com/wolfpld/usenetarchive/"
license=('AGPL3')
makedepends=('git' 'ncurses' 'curl' 'icu' 'gmime' 'meson')
options=('strip' 'zipman')
source=('uat::git+https://github.com/wolfpld/usenetarchive.git')
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	arch-meson uat build
	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
